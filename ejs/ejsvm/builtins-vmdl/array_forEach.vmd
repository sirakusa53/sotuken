#include "../header-vmdl/patterns.vmdh"
#include "../header-vmdl/externc.vmdh"

(needContext, triggerGC, builtinFunction) array_forEach : (Array, callable, JSValue) -> JSValue 
array_forEach(o, callbackfn, thisArg){
  /* 2. 3. */
  int len = number_to_cint(get_jsarray_length(o));
  /* 5. */
  JSValue t = na >= 2 ? thisArg : JS_UNDEFINED;
  /* 6. */
  int k = 0;
  /* 7. */
  while(k < len){
    /* b. */
    if(has_array_element(o, k)){
      /* c. */
      /* i. */
      JSValue js_k = cint_to_number(k);
      JSValue k_value = get_array_element(o, k);
      /* ii. */
      match(callbackfn){
        case(Function callbackfn){
          send_function3(t, callbackfn, k_value, js_k, o);
        }
        case(Builtin callbackfn){
          send_builtin3(t, callbackfn, k_value, js_k, o);
        }
        case(true){
          LOG_EXIT("forEach: this code could not happen");
        }
      }
    }
    /* d. */
    k <- k + 1;
  }
  /* 8. */
  return JS_UNDEFINED;
}
